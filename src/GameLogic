void resetGame() {
  player = new Player(100, groundY - 50, 40, 50);

  enemies = new ArrayList<Enemy>();
  coins = new ArrayList<Coin>();

  score = 0;
  spawnTimer = 0;
  coinSpawnTimer = 0;

  spawnInterval = 120;
  coinSpawnInterval = 90;

  gameSpeed = 4;
}

void updateGame() {

  gameSpeed = 4 + score * 0.07;

  spawnTimer++;

  if (spawnTimer >= spawnInterval) {
    spawnTimer = 0;

    int type = int(random(3));
    Enemy e;

    if (type == 0)
      e = new Enemy(width + 40, groundY - 30, 30, 30, -(gameSpeed + random(2,5)), 0);
    else if (type == 1)
      e = new Enemy(width + 40, groundY - 60, 60, 60, -(gameSpeed + random(0,2)), 1);
    else {
      float fy = groundY - 150 - random(0, 60);
      e = new Enemy(width + 40, fy, 40, 40, -(gameSpeed + random(1,4)), 2);
    }

    enemies.add(e);
    spawnInterval = max(50, int(120 - score * 0.6));
  }


  coinSpawnTimer++;
  if (coinSpawnTimer >= coinSpawnInterval) {
    coinSpawnTimer = 0;

    float cy = groundY - 80 - random(0, 150);
    coins.add(new Coin(width + 20, cy, 12));

    coinSpawnInterval = max(40, int(90 - score * 0.3));
  }


  player.update();


  for (int i = enemies.size()-1; i >= 0; i--) {
    Enemy e = enemies.get(i);
    e.update();

    if (e.x + e.w < -50) {
      enemies.remove(i);
      score++;
      continue;
    }

    if (e.collidesWith(player)) {
      state = "gameover";
      highScore = max(highScore, score);
    }
  }

  for (int i = coins.size()-1; i >= 0; i--) {
    Coin c = coins.get(i);
    c.x -= gameSpeed * 0.8;

    if (c.x + c.r < 0) {
      coins.remove(i);
      continue;
    }

    if (player.collidesWith(c)) {
      coins.remove(i);
      score += 10;
    }
  }
}
